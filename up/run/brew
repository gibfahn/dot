#!/usr/bin/env zsh

set -ex

# Install brew and Xcode Command Line Tools.
if ! type brew; then
  # Should only exist if we're on a work machine.
  if [[ -d ${wrk_dot_dir:-} ]]; then
    "$wrk_dot_dir"/up/run/brew
  else
    { set +x; } 2>/dev/null
    NONINTERACTIVE=1 /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
    set -x
  fi
fi

# Install xcode command line tools (should have already been brew installed).
# If these are messed up run `xcode-select -p`. It will normally print one of:
# - /Library/Developer/CommandLineTools
# - /Applications/Xcode.app/Contents/Developer
if ! xcode-select -p &>/dev/null; then
  xcode-select --install
fi

export PATH=/opt/homebrew/bin:/opt/brew/bin:$PATH

copyq_version_old=$(copyq --version || true)

# Disable quarantine for casks.
export HOMEBREW_CASK_OPTS=--no-quarantine

# Upgrade everything, even things that weren't in your Brewfile.
brew update

# If the cask is installed and outdated.
if brew list --cask | grep -q sogouinput && ! brew outdated sogouinput; then
  # Cask can no longer be upgraded, must be manually reinstalled.
  brew reinstall sogouinput

  sogou_dir="$(brew info sogouinput | awk '/\/Caskroom\/sogouinput\// { print $1 }')"
  open -a "$sogou_dir"/sogou*.app
fi

brew upgrade # You may occasionally want to run `brew upgrade --greedy` in case built-in cask updaters aren't working.

# brew install things. Edit config/Brewfile to adjust.
brew tap Homebrew/bundle

[[ $USER == gib ]] && brewfiles=("$XDG_CONFIG_HOME"/brew/*) \
  || brewfiles=("$XDG_CONFIG_HOME"/brew/Brewfile)

if ! brew bundle --file=<(cat ${brewfiles[@]}) check >/dev/null; then
  brew bundle --file=<(cat ${brewfiles[@]}) | grep -Evx "Using [-_/0-9a-zA-Z ]+"
fi

# Only run this expensive step if stdout and stderr are ttys (script is being run interactively).
if [[ -t 1 && -t 2 ]]; then
  # Useful to see what you need to add to your Brewfiles.
  echo "Brew packages that would be cleaned up, run this to actually clean:
    brew bundle cleanup --force --file=<(cat ${brewfiles[@]})"
  brew bundle cleanup --file=<(cat ${brewfiles[@]})
fi

copyq_version_new=$(copyq --version)

if [[ $copyq_version_old != "$copyq_version_new" ]]; then
  echo "ERROR: CopyQ version changed, you need to manually untick and retick System Preferences -> Security & Privacy -> Accessibility -> CopyQ.app"
  open /System/Library/PreferencePanes/Security.prefPane
fi
