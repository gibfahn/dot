#!/usr/bin/env zsh

set -ex

# Force color on in brew output, even though not writing to a tty.
export HOMEBREW_COLOR=1

# Install brew and Xcode Command Line Tools.
if ! command -v brew >/dev/null; then
  # Should only exist if we're on a work machine.
  if [[ -d ${wrk_dot_dir:-} ]]; then
    "$wrk_dot_dir"/scripts/run/brew
  else
    sudo -v
    { set +x; } 2>/dev/null
    NONINTERACTIVE=1 /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
    set -x
  fi
fi

# Work around <https://github.com/orgs/Homebrew/discussions/5528>, where brew revokes sudo for the
# whole terminal.
# Can't use script as per that issue because it messes up up's indicatif-powered terminal output.
brew() {
  # faketty is installed in the `rust` task, so can't guarantee it is present at this point.
  if command -v faketty >/dev/null; then
    faketty brew "$@"
  else
    command brew "$@"
  fi
}

# Install xcode command line tools (should have already been brew installed).
# If these are messed up run `xcode-select -p`. It will normally print one of:
# - /Library/Developer/CommandLineTools
# - /Applications/Xcode.app/Contents/Developer
if ! xcode-select -p &>/dev/null; then
  xcode-select --install
fi

export PATH=/opt/homebrew/bin:/opt/brew/bin:$PATH

# Disable quarantine for casks.
export HOMEBREW_CASK_OPTS=--no-quarantine

# Upgrade everything, even things that weren't in your Brewfile.
brew upgrade # You may occasionally want to run `brew upgrade --greedy` in case built-in cask updaters aren't working.

# brew install things. Edit config/Brewfile to adjust.
brew tap Homebrew/bundle

brewfiles=("$XDG_CONFIG_HOME"/brew/Brewfile)

if [[ -e "$XDG_CONFIG_HOME"/brew/Brewfile-wrk ]]; then
  brewfiles+=("$XDG_CONFIG_HOME"/brew/Brewfile-wrk)
fi

if [[ -e "$XDG_CONFIG_HOME"/brew/Brewfile-$(whoami) ]]; then
  brewfiles+=("$XDG_CONFIG_HOME"/brew/Brewfile-$(whoami))
fi

# Mac App Store things come last because mas is slow and often broken.
if [[ -e "$XDG_CONFIG_HOME"/brew/Brewfile-mas ]]; then
  brewfiles+=("$XDG_CONFIG_HOME"/brew/Brewfile-mas)
fi

if ! brew bundle --file =(cat ${brewfiles[@]}) check >/dev/null; then
  brew bundle --file =(cat ${brewfiles[@]})
fi

# Only run this expensive step if stdout and stderr are ttys (script is being run interactively).
if [[ -t 1 && -t 2 ]]; then
  # Useful to see what you need to add to your Brewfiles.
  echo "Brew packages that would be cleaned up, run this to actually clean:
    brew bundle cleanup --force --file =(cat ${brewfiles[@]})"
  # Command returns 1 exit code if anything would be cleaned up.
  # <https://github.com/homebrew/homebrew-bundle/commit/34e39886a92e5f14d46fa014d5ed8712934d5820>
  brew bundle cleanup --file =(cat ${brewfiles[@]}) || true
fi
