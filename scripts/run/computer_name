#!/usr/bin/env zsh

set -euo pipefail

set -x

if [[ -n ${1:-} ]]; then
  mac_name=$1

  scutil --set LocalHostName $mac_name
  scutil --set ComputerName $mac_name
  exit
fi

# Skip if there are more than 2 users on the machine.
[[ $(ls /Users | wc -l | tr -d ' ') == 2 ]] || exit 204

user=$(whoami)
User=${(C)user} # Capitalise the current username, e.g. gib -> Gib.

case $(system_profiler SPHardwareDataType | awk '/Model Name:/ { print $3,$NF; }') in
  "MacBook Pro") mac_type=MBP ;;
  "Mac Studio") mac_type=MacStudio ;;
  "Mac Pro") mac_type=MacPro ;;
  *) echo "Unknown Mac type"; exit 1 ;;
esac

# e.g. "MacBook Pro (16-inch, 2021)"
product_name=$(ioreg -l | grep product-name | awk -F'"' '{print $4}')
# Year the Mac was released, e.g. "2021"
year=$(grep -oE '[0-9]{4}' <<<$product_name)

mac_name=$User-$mac_type-$year

work_done= # Did we change anything?

if [[ $(scutil --get LocalHostName) != $User-* ]]; then
  work_done=true
  scutil --set LocalHostName $mac_name
fi

if [[ $(scutil --get ComputerName) != $User-* ]]; then
  work_done=true
  scutil --set ComputerName $mac_name
fi

if [[ -z ${work_done:-} ]]; then
  exit $UP_EXIT_CODE_SKIPPED
fi
