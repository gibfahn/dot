#!/bin/zsh

# Add entries to the /etc/hosts file.

set -euxo pipefail

[[ $USER == gib ]] || exit "$UP_EXIT_CODE_SKIPPED" # User is gib or we don't run.

main() {
  hosts=(
    # Block YouTube
    # https://apple.stackexchange.com/questions/373190/block-youtube-on-safari-in-hosts-file
    youtube.com
    youtu.be
    youtube-nocookie.com

    # Block Twitter
    twitter.com
    # Block Reddit
    reddit.com
  )

  heading_line="# Added by gib dot host blocking script."

  typeset -a hosts_lines=("${(@f)"$(</etc/hosts)"}") # "

  changed=

  for host_name in ${hosts[@]}; do
    for host in $host_name www.$host_name; do
      # Need IPv4 and IPv6 <https://stackoverflow.com/a/73720696/2799191>
      for localhost in 127.0.0.1 ::1; do
        matching_line="$localhost	$host"
        if ! (($hosts_lines[(Ie)$matching_line])); then
          if [[ -z $changed ]] && ! (($hosts_lines[(Ie)$heading_line])); then
            hosts_lines+=("$heading_line")
            hosts_lines+=()
          fi
          changed=true
          echo >&2 "Adding $host to /etc/hosts"
          hosts_lines+=("$matching_line")
        fi
      done
    done
  done

  if [[ -n $changed ]]; then
    sudo tee <<<"${(@F)hosts_lines}" /etc/hosts >/dev/null

    case $OSTYPE in
      darwin*)
        dscacheutil -flushcache
        sudo killall -HUP mDNSResponder
        ;;
      *) echo "Not yet supported, fix $0" && exit 1 ;;
    esac
  else
    exit "$UP_EXIT_CODE_SKIPPED"
  fi
}

main $@
