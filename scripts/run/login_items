#!/usr/bin/env zsh

set -euxo pipefail


# OS is darwin or we don't run.
[[ $OSTYPE == darwin* ]] || exit "$UP_EXIT_CODE_SKIPPED"

changed=

# Set up login items.
IFS=$'\n' current_login_items=($(osascript -e 'tell application "System Events" to get the path of every login item' | sed -e 's/, /\n/g'))

wanted_login_items=()
for file_path in ${XDG_CONFIG_HOME:-$HOME/.config}/login_items/*; do
  wanted_login_items+=("${(@f)"$(<$file_path)"}")
done
declare -p current_login_items wanted_login_items

for item in "${wanted_login_items[@]}"; do
  if [[ ! -e $item ]]; then
    echo "Not adding missing login item $item" >&2
    continue
  fi
  if ! (( ${current_login_items[(I)$item]} )); then
    changed=true
    echo "Adding login item $item" >&2
    osascript -e "tell application \"System Events\" to make login item at end with properties {path:\"$item\", hidden:false}"
  fi
done

for item in "${current_login_items[@]}"; do
  if ! (( ${wanted_login_items[(I)$item]} )); then
    changed=true
    echo "Removing login item $item" >&2
    osascript -e "tell application \"System Events\" to delete login items whose path is \"$item\""
  fi
done

# If we didn't change anything, mark the task as skipped.
if [[ -z $changed ]]; then
  exit "$UP_EXIT_CODE_SKIPPED"
fi
