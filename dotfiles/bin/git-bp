#!/usr/bin/env zsh

set -eu

usage() {
echo "$(tput bold; tput setaf 74)$ZSH_ARGZERO [-h|--help] [-v|--version] [-f|--force] [-q|--quiet]$(tput sgr0)

git branch prune: delete local branches whose commits have already been merged.

Note that it defaults to deleting on enter, hit n to avoid.
"
}

CHECK= QUIET=

while [[ $# != 0 ]]; do
  [[ ${1:-} =~ ^(--help|-h)$ ]] && { usage; exit; }

  [[ ${1:-} =~ ^(--version|-v)$ ]] && { echo "main"; exit; }

  [[ ${1:-} =~ ^(--check|-c)$ ]] && { CHECK=true; shift; }

  [[ ${1:-} =~ ^(--quiet|-q)$ ]] && { QUIET=true; shift; }
done

branches_to_prune=()

log() {
  [[ -z $QUIET ]] && echo "${1:-}" >&2
}

warn() {
  echo "${1:-}" >&2
}

while read branch up_branch; do
  # If no remote-tracking branch with the same name in any remote,
  if [[ -z $(for remote in $(git remote); do git rev-parse --verify --quiet "$remote/$branch" ; done) ]] &&
    # and upstream branch exists,
    [[ -n "$up_branch" ]] &&
    # and upstream branch contains all the commits in fork branch.
    ! git cherry -v "$up_branch" "$branch" | grep -q '^+'; then
    # then we should delete the branch.
    branches_to_prune+=("$branch")
  fi
done <<<"$(git for-each-ref refs/heads --format='%(refname:short) %(upstream:short)')"

if [[ ${#branches_to_prune[@]} = 0 ]]; then
  log "Nothing to prune."
  exit 0
fi

if [[ -z $CHECK ]]; then
  warn "Deleting branches: ${branches_to_prune[*]}"
else
  # Prompt before deleting.
  echo "Branches to delete: ${branches_to_prune[*]}" >&2
  read "choice?Continue? [Y/n] "
  case $choice in
    N|n|no|No|NO) echo "Exiting..."; exit 1 ;;
  esac
fi

current_branch=$(git branch --show-current)
if (( ${branches_to_prune[(I)$current_branch]} )); then
  # Go back to up HEAD branch.
  git update-index --refresh
  # If there are no uncommitted changes:
  if git diff-index --quiet HEAD --; then
    # Take the first up or pub remote we find.
    up_remote=$(git remote | grep -x 'up\|pub' | sort -r | head -1)
    default_branch=$(git default-branch "$up_remote" 2>/dev/null)
    if git rev-parse "$default_branch" &>/dev/null; then
      git checkout --quiet "$default_branch"
      git merge --ff-only
    else
      git checkout --quiet -b "$default_branch" "$up_remote/$default_branch"
    fi
  else
    warn "Can't delete current branch '$current_branch' as it has uncommitted changes."
  fi
fi

git branch -D "${branches_to_prune[@]}"
